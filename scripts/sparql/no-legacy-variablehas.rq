PREFIX gcdfo: <https://w3id.org/gcdfo/salmon#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>

# Guardrail: legacy variable decomposition annotation/property names must not
# reappear in canonical ontology after migration to iadopt* naming.
SELECT DISTINCT ?legacyProperty ?issue ?subject ?object
WHERE {
  VALUES ?legacyProperty {
    gcdfo:variableHasObjectOfInterest
    gcdfo:variableHasProperty
    gcdfo:variableHasConstraint
    gcdfo:variableHasMatrix
    gcdfo:variableHasContextObject
    gcdfo:variableHasStatisticalModifier
  }

  {
    ?legacyProperty a owl:AnnotationProperty .
    BIND("legacy annotation property declared" AS ?issue)
    BIND(?legacyProperty AS ?subject)
    BIND(?legacyProperty AS ?object)
  }
  UNION
  {
    ?legacyProperty a owl:ObjectProperty .
    BIND("legacy object property declared" AS ?issue)
    BIND(?legacyProperty AS ?subject)
    BIND(?legacyProperty AS ?object)
  }
  UNION
  {
    ?subject ?legacyProperty ?object .
    BIND("legacy property used in triples" AS ?issue)
  }
}
ORDER BY ?legacyProperty ?issue ?subject ?object
