PREFIX : <https://w3id.org/dfoc/salmon#>
PREFIX dfoc: <https://w3id.org/dfoc/salmon#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

# Reports ontology terms that are missing dfoc:theme, exceed the 3-theme cap,
# or reference a theme outside :ThemeScheme.
SELECT ?term ?issue ?themeCount
WHERE {
  {
    SELECT ?term (0 AS ?themeCount) ("missing theme" AS ?issue)
    WHERE {
      ?term a ?type .
      FILTER(?type IN (owl:Class, owl:ObjectProperty, owl:DatatypeProperty, owl:AnnotationProperty, skos:Concept, skos:ConceptScheme)) .
      FILTER(!isBlank(?term)) .
      FILTER NOT EXISTS { ?term dfoc:theme ?theme }
    }
  }
  UNION
  {
    SELECT ?term (COUNT(?theme) AS ?themeCount) ("more than 3 themes" AS ?issue)
    WHERE {
      ?term a ?type .
      FILTER(?type IN (owl:Class, owl:ObjectProperty, owl:DatatypeProperty, owl:AnnotationProperty, skos:Concept, skos:ConceptScheme)) .
      FILTER(!isBlank(?term)) .
      ?term dfoc:theme ?theme .
    }
    GROUP BY ?term
    HAVING(COUNT(?theme) > 3)
  }
  UNION
  {
    SELECT ?term (COUNT(?theme) AS ?themeCount) ("theme not in ThemeScheme" AS ?issue)
    WHERE {
      ?term a ?type .
      FILTER(?type IN (owl:Class, owl:ObjectProperty, owl:DatatypeProperty, owl:AnnotationProperty, skos:Concept, skos:ConceptScheme)) .
      FILTER(!isBlank(?term)) .
      ?term dfoc:theme ?theme .
      FILTER NOT EXISTS { ?theme skos:inScheme :ThemeScheme }
    }
    GROUP BY ?term
  }
}
ORDER BY ?issue ?term
